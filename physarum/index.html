<!DOCTYPE html>
<html lang="en" dir="ltr">

<head>
  <meta charset="utf-8">
  <title>Physarum</title>
  <!-- MOBILE––––––––––––––––––––––– -->
  <meta name="viewport" content="width=device-width, initial-scale=1">
  <link href="https://api.fonts.coollabs.io/css2?family=Raleway:wght@300;400;600&display=swap" rel="stylesheet">
  <!-- CSS––––––––––––––––––––––– -->
  <link rel="stylesheet" href="../shared/css/normalize.css">
  <link rel="stylesheet" href="../shared/css/skeleton.css">
  <link rel="stylesheet" href="../shared/css/darkskelleton.css" media="(prefers-color-scheme: dark)">
  <link rel="stylesheet" href="../shared/css/style.css">
  <link rel="icon" type="image/png" href="/images/favicon.png">
</head>

<body>
  <style>
    .container {
      max-width: 1024px;
      /* override default width so canvas doesn't get scaled*/
    }
  </style>
  <div class="container" style="margin-top: 1em;">
    <a href="../">Back to Overview</a>
    <div class="row">
      <div class="column">
        <h1>Physarum</h1>
        <p>
          Physarum Polycephalum is a slime mold that is able to solve shortest path problems, with astonsishing
          efficiency. Inspired by the amazing art of <a href="https://cargocollective.com/sagejenson/physarum">Sage Jenson</a></b>
        </p>
      </div>
    </div>

    <div class="row">
      <p id="webglWarning" style="display: none;">
        <b style="color: red;">Due to WebGL issues, the simulation does not work on your device</b>
      </p>
      <canvas width="1024" height="1024" class="u-full-width" id="canvas"></canvas>
      
      <label>Search Angle
        <input type="range" id="searchangle" value="0.2" min="0" max="1.6" step="any">
        <output for="#searchangle" id="searchangle_out" > </output>
      </label>
      <label>Turn Rate
        <input type="range" id="rotationrate" value="0.35" min="0" max="1.6" step="any">
        <output for="#rotationrate" id="rotationrate_out"> </output>
      </label>
      <label>Random Turn Probability
        <input type="range" id="turnrandom" value="0.2" min="0" max=".5" step="any">
        <output for="#turnrandom" id="turnrandom_out"> </output>
      </label>
      <label>Search Distance
        <input type="range" id="searchdistance" value="5" min="0" max="30" step="any">
        <output for="#searchdistance" id="searchdistance_out"> </output>
      </label>
      <label>Movement Speed
        <input type="range" id="movementspeed" value="1.5" min="0" max="5" step="any">
        <output for="#movementspeed" id="movementspeed_out"> </output>
      </label>
      <label>Evaporation Rate
        <input type="range" id="evaporationrate" value=".8" min="0" max="1" step="any">
        <output for="#evaporationrate" id="evaporationrate_out"> </output>
      </label>
    </div>
  </div>

  <!-- Minimal Vertex Shader -->
  <script id="vs" type="x-shader/x-vertex">
    attribute vec4 a_position;
    
    void main() {
      gl_Position = a_position;
    }
  </script>


  <!-- Point Sprite Vertex Shader -->
  <script id="vs_points" type="x-shader/x-vertex">
    precision highp float;
    precision highp sampler2D;

    attribute vec2 a_Index; // UV index of the sprite
    uniform vec2 u_resolution; // width/height of screen
    uniform sampler2D u_texture;

    void main() {
      vec4 simData = texture2D(u_texture, a_Index);
      vec2 pos = simData.xy;
      gl_Position = vec4(pos*2.-1., 0.0, 1.0);
      gl_PointSize = 1.0;
    }
  </script>

  <!-- Texture Render Shader -->
  <script type="x-shader/x-fragment" id="textureRenderShader">
    precision highp float;
    precision highp sampler2D;
    
    uniform vec2 u_resolution;
    uniform sampler2D u_texture;
      
    void main() {
      vec4 texValue = texture2D(u_texture, gl_FragCoord.xy/u_resolution);
      gl_FragColor = vec4(texValue.rgb,1.);
    }
  </script>

  <!-- Particle Render Shader -->
  <script type="x-shader/x-fragment" id="renderShader">
    precision highp float;      
    void main() {
      //float c = 1. - length( .5 - gl_PointCoord.xy );
      //gl_FragColor=vec4(c, 0.,0.,1.);
      gl_FragColor=vec4(1., 0., 0., .1);
    }
  </script>


  <!-- Pheromone Diffusion Shader -->
  <script type="x-shader/x-fragment" id="diffusionShader">
    precision highp float;
    precision highp sampler2D;
    
    uniform vec2 u_resolution;
    uniform vec2 u_mousePos;
    uniform sampler2D u_texture;
    uniform float u_evaporationRate;

    // write particles to r and g channels, those get cleared
    // diffuse b and w, add r and g to them
    vec2 getPixel(vec2 offset){
      vec2 pheroVal = texture2D(u_texture, (gl_FragCoord.xy+offset)/u_resolution).ba;
      vec2 particleVal = texture2D(u_texture, (gl_FragCoord.xy+offset)/u_resolution).rg;
      return (pheroVal+particleVal);
    }

    float circle(vec2 uv){
      vec2 dist = uv - u_mousePos;
      return 1.-smoothstep(0., .05, dot(dist,dist)*4.0);
    }

    void main() {
     vec2 sum = getPixel(vec2(-1.,-1.))/4. + getPixel(vec2(0.,-1.))/2. + getPixel(vec2(1.,-1.))/4.+
                getPixel(vec2(-1.,0.))/2. + getPixel(vec2(0.)) + getPixel(vec2(1.,0.))/2.+
                getPixel(vec2(-1.,1.))/4. + getPixel(vec2(0.,1.))/2. + getPixel(vec2(1.,1.))/4.;
     vec2 average = sum/4.;
     vec2 mouseVal = vec2(.8,1.)*circle(gl_FragCoord.xy/u_resolution);
     //not strictly necessary to evaporate the original particle trail
     //vec2 particleVal = texture2D(u_texture, (gl_FragCoord.xy)/u_resolution).xy;
     vec4 color = vec4(0.,0., (average + mouseVal)*u_evaporationRate );
    gl_FragColor = clamp(color, 0.001,1.);
    //gl_FragColor = color;
    }
  </script>

  <!-- inital particle attributes Shader -->
  <script type="x-shader/x-fragment" id="initShader">
    precision highp float;
    #define M_PI 3.141592653589793
    
    uniform vec2 u_resolution;
      
    vec2 n2rand(vec2 uv) {
      return vec2(fract(sin(dot(uv.xy, vec2(12.9898, 78.233))) * 43758.5453),
        fract(sin(dot(uv.xy * 1.61803, vec2(12.9898, 78.233))) * 43758.5453));
    }

    void main() {
      vec2 uv = 2.* gl_FragCoord.xy/u_resolution -1.;
      // r,g are x and y coordinates; b stores the direction
      gl_FragColor = vec4( n2rand(uv), n2rand(uv*4.).x*2.*M_PI, 1.);
    }
  </script>

  <!-- update positions and velocities Shader -->
  <script type="x-shader/x-fragment" id="simShader">
    precision highp float;
    precision highp sampler2D;

    #define M_PI 3.141592653589793

    uniform float u_speed;
    uniform float u_time;
    uniform float u_searchAngle;
    uniform float u_searchDistance;
    uniform float u_rotationRate;
    uniform float u_turnrandom;
    uniform vec2 u_resolution;
    //u_texture is the data texture wit particle positions and velocities
    uniform sampler2D u_texture;
    uniform sampler2D u_pheroTexture;

    vec2 n2rand(vec2 uv) {
      return vec2(fract(sin(dot(uv.xy, vec2(12.9898, 78.233))) * 43758.5453),
        fract(sin(dot(uv.xy * 1.61803, vec2(12.9898, 78.233))) * 43758.5453));
    }

    float getPheroAtAngle(float a,vec2 pos, float dir){
      //dir = mod(dir,2.*M_PI);
      vec2 offset = (vec2(cos(dir+a), sin(dir+a))/u_resolution)*u_searchDistance;
      vec4 pheroVal = texture2D(u_pheroTexture, pos+offset);
      return pheroVal.b;
    }
    
    void main() {
      vec4 simData = texture2D(u_texture, gl_FragCoord.xy/u_resolution);
      vec2 pos = simData.xy;
      float dir = simData.z; 

      float pheroL = getPheroAtAngle(-u_searchAngle, pos, dir);
      float pheroC = getPheroAtAngle(0., pos, dir);
      float pheroR = getPheroAtAngle(u_searchAngle, pos, dir);

      /*
      //rotate towards center if at the edge
      if (pos.x>1. || pos.x<0. || pos.y>1. || pos.y<0.) {
        //if (pos.x*pos.x+pos.y*pos.y>1.) {
        float centerDir = atan(-pos.y+.5, -pos.x+.5);
        float delta = mod(centerDir - dir + 3.*M_PI, 2.*M_PI) - M_PI;
        if(delta > 0.){
          dir += .1;
        }else{
          dir -= .1;
        }
      }
      */

      float tmp_speed = u_speed;
      vec2 random = n2rand(vec2(dir,u_time));
      if(u_turnrandom>random.x || pheroC > pheroL && pheroC > pheroR){
        // random turn
        if(random.y>.5){
          dir += u_rotationRate;
        }else{
          dir -= u_rotationRate;
        }
      }else{
        // regular turn
        if (pheroL > pheroR) {
          dir -= u_rotationRate;
        } else if (pheroR > pheroL) {
          dir += u_rotationRate;
        }
      }

			

      vec2 vel = vec2(cos(dir), sin(dir)) * tmp_speed / u_resolution.x;

      vec2 newPos = pos+vel;

      //newPos = clamp(newPos,.0,1.);
      newPos = mod(newPos,1.);
      
      gl_FragColor = vec4(newPos, dir, simData.w);
    }
  </script>

  <script type="text/javascript" src="../shared/twgl.min.js"></script>
  <script type="text/javascript" src="script.js"></script>
</body>


</html>